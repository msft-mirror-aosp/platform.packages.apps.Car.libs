/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.jetbrains.kotlin.android"
    id 'com.android.application'
    id 'aaosApps.buildLogic'
}

android {
    namespace 'com.example.appcard.sample.weather'

    defaultConfig {
        minSdkVersion 31
        targetSdkVersion 34
    }

    signingConfigs {
        platformGoogle {
            storeFile file(gradle.ext.platformGoogleCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        platformAosp {
            storeFile file(gradle.ext.platformAospCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    flavorDimensions 'devMode'
    productFlavors {
        if (file(gradle.ext.platformGoogleCertPath).exists()) {
            platformGoogle {
                isDefault true
                dimension 'devMode'
                signingConfig signingConfigs.platformGoogle
            }
        }
        platformAosp {
            dimension 'devMode'
            signingConfig signingConfigs.platformAosp
        }
    }

    // This is the gradle equivalent of the libs: ["android.car"] in the Android.bp
    useLibrary 'android.car'
}

dependencies {
    implementation libs.androidx.annotation
    implementation libs.androidx.appcompat

    implementation libs.rxjava2.rxandroid
    implementation libs.rxjava2.rxjava
    implementation libs.rxjava2.rxkotlin

    implementation libs.retrofit2
    implementation libs.retrofit2.converter.gson
    implementation libs.retrofit2.adapter.rxjava2
    implementation libs.okhttp3.logging.interceptor

    implementation libs.android.car.ui.lib
    implementation project(":car-app-card-lib")
}
