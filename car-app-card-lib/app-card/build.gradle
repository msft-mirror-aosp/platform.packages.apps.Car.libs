/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.jetbrains.kotlin.android"
    id 'com.android.library'
    alias(libs.plugins.protobuf)
    id 'aaosApps.buildLogic'
    id 'buildSrc.jdk.jdk8'
}

android {
    namespace 'com.android.car.appcard'
    testNamespace 'com.android.car.appcard.test'

    defaultConfig {
        minSdkVersion 31
        targetSdkVersion 34
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled = true
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            // Don't proguard the library itself but provides the consumerProguardFiles
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            proto.srcDirs = ['src/main/java/com/android/car/appcard/protos']
        }
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}"
    }
    generateProtoTasks {
        all().forEach { task ->
            task.builtins {
                java { option 'lite' }
                kotlin { option 'lite' }
            }
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation libs.protobuf.kotlin.lite

    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline-extended:2.28.3'
    androidTestImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'
}
