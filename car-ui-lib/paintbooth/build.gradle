/*
 * Copyright (C) 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'aaosApps.buildLogic'
}

android {
    namespace 'com.android.car.ui.paintbooth'
    defaultConfig {
        applicationId "com.android.car.ui.paintbooth"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
    }

    sourceSets {
        main {
            java {
                filter.excludes = [
                        "com/android/car/ui/paintbooth/overlays/OverlayManagerImpl.java",
                        "com/android/car/ui/paintbooth/currentactivity/ActivityTaskManagerImpl.java",
                ]
            }
        }
    }

    signingConfigs {
        unbundled {
            storeFile file(gradle.ext.unbundledCertPath)
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }
    }

    flavorDimensions "signingkey"
    productFlavors {
        unbundled {
            dimension "signingkey"
            signingConfig signingConfigs.unbundled
        }
    }

    buildTypes {
        debug {
            signingConfig android.buildTypes.release.signingConfig
        }
    }

    // This is the gradle equivalent of the libs: ["android.car"] in the Android.bp
    useLibrary 'android.car'
}

dependencies {
    api project(':oem-token-lib')
    implementation project(':car-ui-lib')
    debugImplementation libs.leakcanary.android
    api libs.androidx.annotation
    api libs.androidx.constraintlayout
    api libs.androidx.recyclerview
    api libs.androidx.preference
    api libs.androidx.asynclayoutinflater
}

android.aaptOptions.additionalParameters "-I", "${projectDir}/../oem-tokens/shared-lib/prebuilt/token-shared-lib.apk"
