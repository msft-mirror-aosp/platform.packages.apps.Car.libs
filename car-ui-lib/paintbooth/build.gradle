/*
 * Copyright (C) 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.android.car.ui.paintbooth"
        minSdkVersion 28
        targetSdkVersion 32
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                filter.excludes = [
                        "com/android/car/ui/paintbooth/overlays/OverlayManagerImpl.java",
                        "com/android/car/ui/paintbooth/currentactivity/ActivityTaskManagerImpl.java",
                ]
            }
        }
    }

    signingConfigs {
        "default" {
            storeFile file(gradle.ext.aaosCertPath)
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }

        platform {
            storeFile file(gradle.ext.platformCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    flavorDimensions "signingkey"
    productFlavors {
        "default" {
            dimension "signingkey"
            signingConfig signingConfigs.default
        }
        platform {
            dimension "signingkey"
            if (file(gradle.ext.platformCertPath).exists()) {
                signingConfig signingConfigs.platform
            }
        }
    }

    // This is the gradle equivalent of the libs: ["android.car"] in the Android.bp
    useLibrary 'android.car'
}

dependencies {
    implementation project(':car-ui-lib')
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
    api 'androidx.annotation:annotation:1.3.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.3'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'androidx.preference:preference:1.2.0'
    api "androidx.asynclayoutinflater:asynclayoutinflater:1.0.0"
}
