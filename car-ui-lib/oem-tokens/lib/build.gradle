/*
 * Copyright (C) 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.gradle.tasks.VerifyLibraryResourcesTask

plugins {
    id 'com.android.library'
    id 'aaosApps.buildLogic'
    id 'buildSrc.jdk.jdk8'
}

android {
    namespace 'com.android.car.oem.tokens'
    testNamespace 'com.android.car.oem.tokens.test'

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 33
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            /* Enabling this causes build failures.
            * While we normally want to include Android Resources in unit tests,
            * we don't have support for shared library resources yet. (b/292279181)
            * Either way, this library has no unit tests right now.
            */
            // includeAndroidResources = true
        }
        animationsDisabled = true
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    api 'androidx.annotation:annotation:1.5.0'
    api 'androidx.appcompat:appcompat:1.4.1'

    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
}

// Disable the ':oem-token-lib:verifyReleaseResources' task as references from the shared library
// dependency are not recognized. Rely on resource verification at the binary level. Android Gradle
// plugin support for shared library dependencies tracked at b/292279181.
tasks.withType(VerifyLibraryResourcesTask).configureEach({ task -> task.enabled = false
})

android.aaptOptions.additionalParameters "-I", "${projectDir}/../shared-lib/prebuilt/token-shared-lib.apk"

