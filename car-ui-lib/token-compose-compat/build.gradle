import com.android.build.gradle.tasks.VerifyLibraryResourcesTask

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'aaosApps.buildLogic'
    alias(libs.plugins.compose.compiler)
}

android {
    namespace 'com.android.car.oem.tokens.compose.compat'

    defaultConfig {
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.0"
    }

    buildFeatures {
        compose true
    }
}

dependencies {
    implementation platform(libs.compose.bom)
    androidTestImplementation platform(libs.compose.bom)

    api project(':oem-token-lib')
    implementation libs.androidx.core.ktx
    implementation libs.compose.ui
    implementation libs.compose.material3
    implementation libs.material

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}

// Disable the ':oem-token-lib:verifyReleaseResources' task as references from the shared library
// dependency are not recognized. Rely on resource verification at the binary level. Android Gradle
// plugin support for shared library dependencies tracked at b/292279181.
tasks.withType(VerifyLibraryResourcesTask).configureEach({ task -> task.enabled = false
})

android.aaptOptions.additionalParameters "-I", "${projectDir}/../oem-tokens/shared-lib/prebuilt/token-shared-lib.apk"
