/*
 * Copyright (C) 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Library-level build file

plugins {
    id 'com.android.library'
    id 'aaosApps.buildLogic'
    id 'aaosApps.localPublish'
}

android {
    namespace 'com.android.car.ui'
    testNamespace 'com.android.car.ui.test'


    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 2
        versionName "2.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro', 'proguard-rules-platform.pro'
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-overlayable',
                    'src/main/res-private'
            ]
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled = true
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            // Don't proguard the library itself but provides the consumerProguardFiles
            minifyEnabled false
        }
    }

    project.gradle.taskGraph.whenReady {
        connectedDebugAndroidTest {
            ignoreFailures = true
        }
    }

    buildFeatures {
        buildConfig = false
    }

    // This is the gradle equivalent of the libs: ["android.car"] in the Android.bp
    useLibrary 'android.car'

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    compileOnly project(':oem-apis')
    api project(':car-rotary-lib')
    api 'androidx.annotation:annotation:1.3.0'
    api 'androidx.activity:activity:1.6.0'
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.3'
    api 'androidx.preference:preference:1.2.0'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'androidx.core:core:1.9.0'
    api 'androidx.asynclayoutinflater:asynclayoutinflater:1.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'

    androidTestImplementation project(':car-ui-lib-testing')
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "org.mockito:mockito-core:2.28.1"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    // This is needed to be able to spy certain classes with Mockito
    // It's major/minor version must match Mockito's.
    androidTestImplementation libs.dexmaker.mockito.inline
    // Required for instrumented tests
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
}
