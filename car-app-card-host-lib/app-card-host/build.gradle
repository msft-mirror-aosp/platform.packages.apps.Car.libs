/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.jetbrains.kotlin.android"
    id 'com.android.library'
    id "com.google.protobuf"
    id 'aaosApps.buildLogic'
}

android {
    namespace 'com.android.car.appcard.host'
    testNamespace 'com.android.car.appcard.host.test'

    defaultConfig {
        minSdkVersion 31
        targetSdkVersion 34
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled = true
    }

    project.gradle.taskGraph.whenReady {
        connectedDebugAndroidTest {
            ignoreFailures = true
        }
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}"
    }
    generateProtoTasks {
        all().forEach { task ->
            task.builtins {
                java { option 'lite' }
                kotlin { option 'lite' }
            }
        }
    }
}

dependencies {

    // Required to access android's system APIs
    compileOnly files(gradle.ext.lib_system_stubs)

    implementation project(":car-app-card-lib")
    implementation libs.androidx.annotation
    implementation libs.guava
    implementation libs.protobuf.kotlin.lite

    androidTestCompileOnly files(gradle.ext.lib_system_stubs)
    androidTestImplementation libs.google.truth
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.mockito.core
    androidTestImplementation libs.dexmaker.mockito.inline.extended
    androidTestImplementation libs.mockito.kotlin
}
