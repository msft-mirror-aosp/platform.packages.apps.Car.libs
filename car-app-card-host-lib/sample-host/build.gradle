/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.gradle.tasks.VerifyLibraryResourcesTask

plugins {
    id 'org.jetbrains.kotlin.android'
    id 'com.android.application'
    id 'aaosApps.buildLogic'
    alias(libs.plugins.compose.compiler)

}

android {
    namespace 'com.example.appcard.samplehost'

    defaultConfig {
        minSdkVersion 31
        targetSdkVersion 34
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
    }

    signingConfigs {
        platformGoogle {
            storeFile file(gradle.ext.platformGoogleCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        platformAosp {
            storeFile file(gradle.ext.platformAospCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    flavorDimensions 'devMode'
    productFlavors {
        // Keep platform flavors to allow for quick development iteration
        if (file(gradle.ext.platformGoogleCertPath).exists()) {
            platformGoogle {
                isDefault true
                dimension 'devMode'
                signingConfig signingConfigs.platformGoogle
            }
        }
        platformAosp {
            dimension 'devMode'
            signingConfig signingConfigs.platformAosp
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            signingConfig android.buildTypes.release.signingConfig
        }
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        compose true
    }
}

dependencies {
    implementation project(":car-app-card-host-lib")
    implementation project(":car-app-card-lib")
    implementation project(":token-compose-compat")
    implementation libs.androidx.annotation
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.ui
    implementation libs.androidx.material3

    // Compose dependencies
    def composeBom = libs.compose.bom
    implementation composeBom
    implementation libs.compose.runtime.livedata
}

// Disable the ':oem-token-lib:verifyReleaseResources' task as references from the shared library
// dependency are not recognized. Rely on resource verification at the binary level. Android Gradle
// plugin support for shared library dependencies tracked at b/292279181.
tasks.withType(VerifyLibraryResourcesTask).configureEach({ task -> task.enabled = false
})

android.aaptOptions.additionalParameters "-I", "${projectDir}/../../car-ui-lib/oem-tokens/shared-lib/prebuilt/token-shared-lib.apk"
