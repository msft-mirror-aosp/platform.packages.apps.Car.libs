/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.gradle.tasks.VerifyLibraryResourcesTask

plugins {
    id 'org.jetbrains.kotlin.android'
    id 'com.android.application'
    id 'buildSrc.jdk.jdk8'
}

android {
    namespace 'com.example.appcard.samplehost'

    compileSdkVersion gradle.ext.aaosLatestSDK
    defaultConfig {
        minSdkVersion 31
        targetSdkVersion gradle.ext.aaosTargetSDK
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
    }

    signingConfigs {
        aaos {
            storeFile file(gradle.ext.aaosCertPath)
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }

        platform {
            storeFile file(gradle.ext.platformCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    flavorDimensions 'devMode'
    productFlavors {
        prod {
            dimension 'devMode'
            signingConfig signingConfigs.aaos
        }

        platform {
            dimension 'devMode'
            signingConfig signingConfigs.platform
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            signingConfig android.buildTypes.release.signingConfig
        }
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = composeCompilerVersion
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation project(":car-app-card-host-lib")
    implementation project(":car-app-card-lib")
    implementation project(":token-compose-compat")

    // Compose dependencies
    def composeBom = platform('androidx.compose:compose-bom:2024.02.00')
    implementation composeBom
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation "androidx.compose.ui:ui"
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.runtime:runtime-livedata'
}

// Disable the ':oem-token-lib:verifyReleaseResources' task as references from the shared library
// dependency are not recognized. Rely on resource verification at the binary level. Android Gradle
// plugin support for shared library dependencies tracked at b/292279181.
tasks.withType(VerifyLibraryResourcesTask).configureEach({ task -> task.enabled = false
})

android.aaptOptions.additionalParameters "-I", "${projectDir}/../../car-ui-lib/oem-tokens/shared-lib/prebuilt/token-shared-lib.apk"
